allprojects  {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  group = 'io.moquette'
  version = '0.13.10-SNAPSHOT'
}

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    classpath 'gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0'
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
  }
}

subprojects {

  repositories {
    mavenLocal()

    maven {
      url 'https://buddy-platform.pkgs.visualstudio.com/_packaging/airstreampkg/maven/v1'
    }
    maven { url "https://repo.eclipse.org/content/repositories/paho-releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://repo.maven.apache.org/maven2" }
  }

  apply from: new File(rootDir, 'gradle/java.gradle')
  apply from: new File(rootDir, 'gradle/test.gradle')
  apply from: new File(rootDir, 'gradle/lint.gradle')
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'eclipse'

  ext {
    nettyVersion = '4.1.22.Final'
  }

  task packageTests(type: Jar) {
    from sourceSets.test.output
    from sourceSets.test.allSource
    from sourceSets.test.resources
    classifier = 'tests'
  }

  task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  task sourceJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task fatJar(type: Jar) {
    manifest {
      attributes 'Implementation-Title': 'Gradle Jar File Example',
              'Implementation-Version': version,
              'Main-Class': 'com.mkyong.DateUtils'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
  }

  // summarize artifacts
  artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
    archives packageTests
  }

  dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.5'

    testCompile group: 'junit', name: 'junit', version:'4.8.2'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version:'1.7.5'
    testCompile group: 'org.assertj', name: 'assertj-core', version:'2.6.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.9.0'
    testCompile group: 'org.awaitility', name: 'awaitility', version: '3.0.0'
  }

  test {
    systemProperties.put "moquette.path", "build/resources/test/"
    systemProperties.put "io.netty.leakDetectionLevel", "paranoid"
    testLogging {
      exceptionFormat 'full'
      events "passed", "skipped", "failed"
    }
  }

  // Deploy set environment variables and run ./gradlew bintrayUpload
  // publication to maven repo
  publishing {
    publications {
      myPublication(MavenPublication) {
        groupId 'io.moquette'
        artifactId 'moquette'
        version '0.13.10-SNAPSHOT'
        artifact "build/libs/${project.name}-all-${version}.jar"
      }
    }

    repositories {
      maven {
        url 'https://buddy-platform.pkgs.visualstudio.com/_packaging/airstreampkg/maven/v1'
        credentials {
          username "AZURE_ARTIFACTS"
          password System.getenv("AZURE_ARTIFACTS_ENV_ACCESS_TOKEN")
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
}
